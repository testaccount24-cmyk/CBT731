         TITLE 'TOOL TO MANIPULATE THE TSO RELOGON BUFFER'
* ------------------------------------------------------------------- *
* 'BYE' TSO COMMAND                                                   *
*                             WRITTEN JULY 15 2017 (BY CBT UPDATER)   *
*                             MODIFIED  JULY 16 2017 (BY SBG)         *
* ------------------------------------------------------------------- *
* PURPOSE:  TO LOAD THE TSO RELOGON BUFFER WITH ANY DATA (SHOULD BE   *
*           A LOGON/LOGOFF COMMAND) AND SET THE ECT RELOGON SWITCH    *
*           ON, SO LOGOFF IS DONE WHEN IN "READY" MODE, FOLLOWED      *
*           BY THE LOGON/LOGOFF COMMAND IN THE RELOGON BUFFER.        *
* ------------------------------------------------------------------- *
* SYNTAX :      BYE   COMMAND TO PUT IN BUFFER                        *
*                                                                     *
*       RESULT:   PUTS THE COMMAND CHARACTERS INTO THE RELOGON        *
*                 BUFFER, SETS THE BUFFER HEADER, AND SETS            *
*                 THE ECT RELOGON SWITCH "ON".                        *
*                                                                     *
* SYNTAX:       BYE                    (WITH NO OPERANDS)             *
*                                                                     *
*       RESULT:   CLEARS THE RELOGON BUFFER TO BLANKS,                *
*                 INITIALIZES THE RELOGON BUFFER HEADER,              *
*                 AND SETS THE ECT RELOGON SWITCH "OFF".              *
*                 (UN-DO THE RESULT OF "BYE" WITH OPERANDS.)          *
* ------------------------------------------------------------------- *
* DESCRIPTION OF THE PROGRAM:                                         *
*                                                                     *
* THIS MUST BE RUN AS A TSO COMMAND. R1 SHOULD POINT TO THE CPPL.     *
* THIS TSO COMMAND SETS THE ECTLOGF BIT ON IN THE ECT, AND            *
* PUTS THE COMMAND NAME IN THE RELOGON BUFFER, OR, IF ANY OPERANDS    *
* ARE SPECIFIED, PUTS JUST THOSE OPERANDS IN THE RELOGON BUFFER.      *
* ------------------------------------------------------------------- *
* AS A RESULT OF TURNING ON THE ECTLOGF BIT, THE NEXT TIME            *
* A 'READY' PROMPT IS EXPECTED FROM THE TERMINAL MONITOR PROGRAM,     *
* THE TERMINAL MONITOR PROGRAM WILL END INSTEAD, LOGGING THE SESSION  *
* OFF.  WHEN THIS COMMAND IS ISSUED FROM ISPF, THE LOGOFF WILL NOT    *
* OCCUR UNTIL THE USER EXITS FROM ISPF.                               *
* ------------------------------------------------------------------- *
* IF THE OPERAND IS 'LOGOFF' THE SESSION WILL NOT TRY TO LOGON AFTER  *
* IT ENDS. IF THE OPERAND IS 'LOGON', THE SESSION WILL TRY TO LOGON   *
* AFTER IT ENDS, JUST LIKE WHAT HAPPENS IF YOU ENTER 'LOGON' FROM     *
* THE 'READY' PROMPT.  YOU CAN ENTER LOGON USERID/PASSWORD            *
* AS AN OPERAND OF BYE.                                               *
* ------------------------------------------------------------------- *
* IF THE OPERAND IS ABSENT, EVERYTHING IS RE-INITIALIZED,  (SBG)      *
* AND THE ECTLOGF BIT IS SWITCHED OFF, CANCELLING THE RELOGON.        *
* ------------------------------------------------------------------- *
* IF THE OPERAND IS ANYTHING OTHER THAN 'LOGOFF' OR 'LOGON',          *
* THEN AFTER THE LOGOFF THESE MESSAGES WILL APPEAR:                   *
*  IKJ56410I XXX COMMAND NOT ACCEPTED DURING LOGON                    *
*  IJ56400A ENTER LOGON OR LOGOFF-                                    *
* WHERE XXX WILL WILL BE THE COMMAND OR OPERAND THAT WAS SPECIFIED.   *
* ------------------------------------------------------------------- *
* OR IF THE OPERAND DOES NOT EVEN QUALIFY AS A VALID TSO COMMAND      *
* NAME, I.E. MORE THAN 8 CHARACTERS, STARTS WITH A NUMBER, ETC.       *
* THEN AFTER THE LOGOFF THESE MESSAGES WILL APPEAR:                   *
*  IKJ56401I INVALID COMMAND SYNTAX                                   *
*  IJ56400A ENTER LOGON OR LOGOFF-                                    *
* ------------------------------------------------------------------- *
* MODIFICATION:  ("BYE" WITHOUT OPERANDS, RE-INITIALIZES EVERYTHING.) *
*                                                                     *
* FIXED "BYE" WITHOUT OPERANDS, TO INITIALIZE THE RELOGON BUFFER,     *
*  AND TURN THE LOGOFF BIT OFF IN THE ECT.    (SBG)                   *
* ------------------------------------------------------------------- *
*
BYE      CSECT
         USING *,R6
ENTRY0   EQU   *
         SAVE  (14,12),,*
         LR    R6,R15              BASE REGISTER
         LA    R15,SAVE            NEW SAVE AREA
         ST    R13,4(,R15)         OLD ADDRESS IN NEW SAVE AREA
         ST    R15,8(,R13)         NEW ADDRESS IN OLD SAVE AREA
         LR    R13,R15             NEW SAVE AREA
         SPACE
* ------------------------------------------------------------------- *
*      ARE WE A LEGITIMATE TSO COMMAND RUNNING IN THE FOREGROUND?     *
* ------------------------------------------------------------------- *
         TM    0(R1),X'80'         IF PARM INSTEAD OF CPPL
         BO    EXIT                   DON'T DO ANYTHING
         LM    R2,R5,0(R1)         R2 -> CBUF, R4 -> PSCB, R5 -> ECT
         TM    ECTSWS-ECT(R5),ECTBKGRD  IF BACKGROUND
         BO    EXIT                   DON'T DO ANYTHING ELSE
* ------------------------------------------------------------------- *
*      YES WE ARE, SO TURN SWITCH ON AND LOAD THE RELOGON BUFFER      *
* ------------------------------------------------------------------- *
         OI    ECTSWS-ECT(R5),ECTLOGF   TURN ON RELOGON SWITCH
*               COPY THE COMMAND IN CBUF, OR JUST ITS OPERANDS IF ANY
*               TO THE RELOGON BUFFER
         L     R8,PSCBRLGB-PSCB(,R4)    POINT R8 TO RELOGON BUFFER
         MVI   4(R8),C' '               FILL BUFFER + 4 LENGTH 252
         MVC   5(251,R8),4(R8)          WITH BLANKS
         LA    R7,4(,R2)           POINT R7 TO CBUF+4
         LH    R14,0(,R2)          GET LENGTH OF CBUF
         SH    R14,=H'4'           MINUS 2 LEADING HALFWORDS
         BNP   EXIT                SHOULDN'T BE ZERO OR LESS
* ------------------------------------------------------------------- *
* --------------------------------   TEST IF ANY OPERANDS   --------- *
         TM    ECTSWS-ECT(R5),ECTNOPD   IF NO OPERANDS,
         BO    NOOFFS                   RE-INITIALIZE EVERYTHING
* ------------------------------------------------------------------- *
* --------------------------------   LOAD THE RELOGON BUFFER  ------- *
         AH    R7,2(,R2)           POINT TO OPERAND BY ADDING OFFSET
         SH    R14,2(,R2)          REDUCE LENGTH BY SUBTRACTING OFFSET
         BCTR  R14,0               LENGTH MINUS 1 FOR EX
         B     *+10
         MVC   4(0,R8),0(R7)
         EX    R14,*-6             COPY CBUF TO RELOGON BUFFER
         AH    R14,=H'5'           ADD 4 TO LENGTH, PLUS 1 FOR BCTR
         STH   R14,0(,R8)          SET LENGTH IN RELOGON BUFFER
         SR    R14,R14
         STH   R14,2(,R8)          ZERO THE OFFSET HALFWORD
* ------------------------------------------------------------------- *
EXIT     SR    R15,R15             RETURN CODE ZERO
         L     R13,4(,R13)         RESTORE R13
         L     R14,12(,R13)
         RETURN (0,12)
* ------------------------------------------------------------------- *
*   WHEN "BYE" HAS NO OPERANDS, THEN INITIALIZE THE RELOGON BUFFER.   *
* ------------------------------------------------------------------- *
NOOFFS   DS    0H
         MVI   0(R8),X'01'         INITIALIZE LENGTH TO X'100'
         MVI   1(R8),X'00'
         SR    R14,R14
         STH   R14,2(,R8)          ZERO THE OFFSET HALFWORD
         NI    ECTSWS-ECT(R5),X'FF'-ECTLOGF   TURN LOGOFF BIT OFF
*                                  BUFFER WAS ALREADY BLANKED, ABOVE.
         B     EXIT
* ------------------------------------------------------------------- *
*                          CONSTANTS                                  *
* ------------------------------------------------------------------- *
         DC    0D'0'               DOUBLEWORD ALIGNMENT
SAVE     DC    18F'0'              SAVE AREA
         SPACE
         PRINT GEN
         IKJCPPL
         IKJPSCB
         IKJECT
         YREGS
         END   ENTRY0
